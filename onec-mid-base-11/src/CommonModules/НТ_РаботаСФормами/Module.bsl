Процедура ДополнитьФорму(Форма) Экспорт 

	ИмяФормы = Форма.ИмяФормы;
	
	Если СтрНайти(ИмяФормы, "ФормаДокумента") <> 0 Тогда 
		Если СтрНайти(ИмяФормы, "Документ.ОплатаОтПокупателя.Форма.ФормаДокумента") <> 0 Тогда 
			Элемент = Форма.Элементы.Вставить("НТ_КонтактноеЛицо", Тип("ПолеФормы"), ,Форма.Элементы.Основание);
		ИначеЕсли СтрНайти(ИмяФормы, "Документ.ОплатаПоставщику.Форма.ФормаДокумента") <> 0 Тогда 
			Элемент = Форма.Элементы.Вставить("НТ_КонтактноеЛицо", Тип("ПолеФормы"), ,Форма.Элементы.СуммаДокумента);
		Иначе	
			Элемент = Форма.Элементы.Добавить("НТ_КонтактноеЛицо", Тип("ПолеФормы"), Форма.Элементы.ГруппаШапкаПраво);
		КонецЕсли;
		
		Элемент.Заголовок = "Контактное лицо";
		Элемент.ПутьКДанным = "Объект.НТ_КонтактноеЛицо";
		Элемент.Вид = ВидПоляФормы.ПолеВвода;
	КонецЕсли;
	
	Если ИмяФормы = "Документ.ЗаказПокупателя.Форма.ФормаДокумента" Тогда
		Элемент = Форма.Элементы.Добавить("НТ_СогласованнаяСкидка", Тип("ПолеФормы"), Форма.Элементы.ГруппаШапкаЛево);
		Элемент.Заголовок = "Согласованная скидка";
		Элемент.ПутьКДанным = "Объект.НТ_СогласованнаяСкидка";
		Элемент.Вид = ВидПоляФормы.ПолеВвода;
		Элемент.УстановитьДействие("ПриИзменении", "НТ_СогласованнаяСкидкаПриИзменении");
		
		Команда = Форма.Команды.Добавить("НТ_ПересчитатьСуммы");
		Команда.Действие = "НТ_ПересчитатьСуммы";
		
		Кпопка = Форма.Элементы.Добавить("НТ_ПересчитатьСуммы", Тип("КнопкаФормы"), Форма.КоманднаяПанель);
		Кпопка.Заголовок = "Пересчитать суммы";
		Кпопка.Вид = ВидКнопкиФормы.КнопкаКоманднойПанели;
		Кпопка.ИмяКоманды = "НТ_ПересчитатьСуммы";
	КонецЕсли;
	
КонецПроцедуры